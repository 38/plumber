#!/usr/bin/env pscript
import("daemon");
import("options");

var template = Options.empty_template();
Options.add_option(template, "--help", "-h", "Show this help message", 0, 0);
var result = Options.parse(template, argv);

var show_help = function(code, message) 
{
	if(message != undefined) print(message);
	print("Stop the running daemons");
	print("Usage: \n\t", argv[0], " [daemon-ids]");
	print("Arguments:");
	Options.print_help(template);
	exit(code);
}
if(result == undefined) show_help(1, undefined);
if(result["parsed"]["--help"] != undefined) show_help(0, undefined);
if(len(result["unparsed"]) == 0) show_help(1, "Missing daemon IDs");

argv = result["unparsed"];

for(var i = 0; i < len(argv); i ++)
{
	var daemon = argv[i];
	if(Daemon.ping(daemon))
	{
		print("Stopping daemon ", daemon);
		Daemon.stop(daemon);
	}
	else
		print("Cannot find daemon ", daemon);
}
